%option noyywrap
%option stack

%{
    /* --------------------------- INCLUDES ----------------------- */    

    #include "functionsLatex.h"
    #include "utilities.h"
    #include "LinkedList/linkedlist.h"


    /* --------------------------- VAR E ESTRUTURAS ---------------- */    

    LinkedList closeTags;   /* Lista ligada com as tags que estão por fechar */
    LinkedList table;
            
%}
    /* --------------------------- ESTADOS ------------------------- */  

%x VERB
%x VERB_LINE
%x COMMENT_LINE
%x COMMENT
%x IMG  
%x LINK 
%x LIST
%x DICTIONARY
%x HEADER
%x LATEX
%x TABLE

%%
    
    /* --------------------------- HEADER -------------------------- */    

<INITIAL>%%header%               { yy_push_state(HEADER); }               /* Começa o estado de header */
<HEADER>%header%%                { yy_pop_state(); }                      /* Volta ao estado inicial */

<HEADER>%title[ \t].*            { addTitleLatex(yytext+7); }             /* Título */
<HEADER>%author[ \t].*           { addAuthorLatex(yytext+8); }            /* Autor */
<HEADER>%date[ \t].*             { addDateLatex(yytext+6); }              /* Data */

    /* --------------------------- FORMATACAO ---------------------- */    

<INITIAL,LIST,DICTIONARY>%b\{   { beginBoldLatex(closeTags); }            /* Início de negrito */
<INITIAL,LIST,DICTIONARY>%i\{   { beginItalicLatex(closeTags); }          /* Início de italico */
<INITIAL,LIST,DICTIONARY>%u\{   { beginUnderlineLatex(closeTags); }       /* Início de sublinhado */

    /* --------------------------- TITULOS ------------------------- */    

<INITIAL>%t[1-3]\{               { char* str = substring(yytext, 2, 1);    /* Início de título */
                                   beginTitleLatex(closeTags,atoi(str));
                                   free(str) ;
                                 }

    /* --------------------------- INDICE E CAPA ------------------- */    

<INITIAL>^[ \t]*%index%          { addIndexLatex(); }                      /* Índice */
<INITIAL>^[ \t]*%cover%          { addCoverLatex(); }                      /* Capa */

    /* --------------------------- LISTAS -------------------------- */    

<INITIAL,LIST,DICTIONARY>%%ol%   { yy_push_state(LIST);                    /* Começa o estado de lista */
                                   beginOrderListLatex();                  /* Início de lista ordenada */
                                 }                                        
<LIST>%ol%%                      { yy_pop_state();                         /* Volta ao estado anterior */
                                   endOrderListLatex();                    /* Fim de lista ordenada*/
                                 }                                         

<INITIAL,LIST,DICTIONARY>%%ul%   { yy_push_state(LIST);                    /* Começa o estado de lista */
                                   beginUnorderListLatex();                /* Início de lista não ordenada */
                                 }
<LIST>%ul%%                      { yy_pop_state();                         /* Volta ao estado anterior */
                                   endUnorderListLatex();                  /* Fim de lista não ordenada */
                                 }

<INITIAL,LIST,DICTIONARY>%%dl%   { yy_push_state(DICTIONARY);              /* Começa o estado de dicionário*/
                                   beginDictionaryLatex();                 /* Início de entradas de um dicionário */
                                 }
<DICTIONARY>%dl%%                { yy_pop_state();                         /* Volta ao estado anterior */
                                   endDictionaryLatex();                   /* Fim de entradas de um dicionario */
                                 }

    /* --------------------------- ITEMS -------------------------- */    

<DICTIONARY>%it\{                { beginDictionaryEntryLatex(closeTags); } /* Início de um item de dicionário */  

<LIST>%it[ \t]                   { addItemLatex(); }                       /* Item de uma lista */ 

    /* --------------------------- IMAGENS -------------------------- */    

<INITIAL,LIST,DICTIONARY>%img\{  { yy_push_state(IMG);                     /* Começa o estado de imagem */
                                   beginImageLatex(closeTags);             /* Tags de início de uma imagem */
                                 }

<IMG>,                           { endImagePathLatex();                    /* Fim do includegraphics */
                                   beginImageCaptionLatex();               /* Início da legenda */
                                   yy_pop_state();                         /* Volta ao estado anterior do Latex */
                                 }

    /* --------------------------- LINKS -------------------------- */    

<INITIAL,LIST,DICTIONARY>%link\{ { yy_push_state(LINK);                    /* Começa o estado de link */
                                   beginLinkLatex(closeTags);              /* Tags de início de um link */
                                 }

<LINK>,                          { endURLLatex();                          /* Fim do url */
                                   beginLinkTextLatex();                   /* Início do texto do link */
                                   yy_pop_state();                         /* Volta ao estado anterior do Latex */
                                 }

    /* --------------------------- COMENTARIOS -------------------- */    

<INITIAL,LIST,DICTIONARY>%%%     { addLineCommentLatex();                  /* Tag de comentário de linha */
                                   yy_push_state(COMMENT_LINE);            /* Começa o estado de comentário */
                                 }
<COMMENT_LINE>\n                 { yy_pop_state(); }                       /* Volta ao estado anterior*/

<INITIAL,LIST,DICTIONARY>%%comment%  { 
                                   beginCommentLatex();                    /* Início de comentário */
                                   yy_push_state(COMMENT);                 /* Começa o estado de comentário */
                                 }
<COMMENT>%comment%%              { endCommentLatex();                      /* Fim de comentário */
                                   yy_pop_state();                         /* Volta ao estado anterior*/
                                 }

    /* --------------------------- VERBATIM ---------------------- */    

<INITIAL,LIST,DICTIONARY>%%verbatim% { 
                                   yy_push_state(VERB);                    /* Começa o estado verbatim */
                                   beginVerbatimLatex();                   /* Inicio de verbatim */
                                 }
<VERB>%verbatim%%                { yy_pop_state();                         /* Volta ao estado anterior do Latex */
                                   endVerbatimLatex();                     /* Fim de verbatim */
                                 }

<INITIAL,LIST,DICTIONARY>%%verb% { yy_push_state(VERB_LINE);               /* Começa o estado verbatim de linha */
                                   beginVerbatimLineLatex();               /* Inicio de verbatim de linha */
                                 }
<VERB_LINE>%verb%%               { yy_pop_state();                         /* Volta ao estado anterior do Latex */
                                   endVerbatimLineLatex();                 /* Fim de verbatim de linha */
                                 }

    /* --------------------------- LATEX E HTML --------------------- */    

<INITIAL>%%latex%                { yy_push_state(LATEX); }                 /* Começa estado LATEX */
<LATEX>%latex%%                  { yy_pop_state(); }                       /* Volta ao estado anterior */
                         
<INITIAL>%%html%(.|\n)*%html%%   { }                                       /* Ignora */


    /* --------------------------- TABELAS ------------------------- */    

<INITIAL>%%table%               { BEGIN TABLE; }                            /* Começa estado tabela */
<TABLE>%table%%                 { addTableLatex(table);                     /* Cria tabela */
                                  BEGIN INITIAL;
                                }
<TABLE>%head[ \t][^%\n]*        { newRowLatex(table,yytext+6,1); }          /* Nova linha header */
<TABLE>%row[ \t][^%\n]*         { newRowLatex(table,yytext+5,0); }          /* Nova linha */
<TABLE>%[ \t][^%\n]*            { newColumnLatex(table,yytext+1); }         /* Nova Coluna */

    /* --------------------------- GERAL --------------------------- */    

<INITIAL,LIST,DICTIONARY>\}      { endLatexTag(closeTags); }               /* Fecha ultima tag aberta */
<*>.|\n                          { ECHO; }

%%

int main(){
    closeTags = createLinkedList(NULL,NULL);
    table = createLinkedList(NULL,NULL);

    beginDocumentLatex();
    
    yylex();

    endDocumentLatex();
    
    return 0;
}

