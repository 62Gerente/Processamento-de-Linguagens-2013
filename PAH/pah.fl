%option noyywrap
%{
    /* --------------------------- INCLUDES ----------------------- */    

    #include "functionsLatex.h"
    #include "utilities.h"
    #include "LinkedList/linkedlist.h"


    /* --------------------------- VAR E ESTRUTURAS ---------------- */    

    LinkedList closeTags;   /* Lista ligada com as tags que estão por fechar */
    LinkedList table;
            
%}
    /* --------------------------- ESTADOS ------------------------- */  

%x VERB
%x VERB_LINE
%x COMMENT_LINE
%x COMMENT
%x IMG  
%x LINK 
%x LIST
%x DICTIONARY
%x HEADER
%x LATEX
%x TABLE

%%
    /* --------------------------- VARIAVEIS ----------------------- */    
%{   
    char delimiter;       /* Delimitador do verbatim de linha */    
%}

    /* --------------------------- LATEX --------------------------- */    
    
    /* --------------------------- HEADER -------------------------- */    

<INITIAL>%%header%               { BEGIN HEADER; }                        /* Começa o estado de header */
<HEADER>%header%%                { BEGIN INITIAL; }                       /* Volta ao estado inicial */

<HEADER>%title[ \t].*            { addTitleLatex(yytext+7); }             /* Título */
<HEADER>%author[ \t].*           { addAuthorLatex(yytext+8); }            /* Autor */
<HEADER>%date[ \t].*             { addDateLatex(yytext+6); }              /* Data */

    /* --------------------------- FORMATACAO ---------------------- */    

<INITIAL,LIST,DICTIONARY>%b\{   { beginBoldLatex(closeTags); }            /* Início de negrito */
<INITIAL,LIST,DICTIONARY>%i\{   { beginItalicLatex(closeTags); }          /* Início de italico */
<INITIAL,LIST,DICTIONARY>%u\{   { beginUnderlineLatex(closeTags); }       /* Início de sublinhado */

    /* --------------------------- TITULOS ------------------------- */    

<INITIAL>%t[1-3]\{               { char* str = substring(yytext, 2, 1);    /* Início de título */
                                   beginTitleLatex(closeTags,atoi(str));
                                   free(str) ;
                                 }

    /* --------------------------- INDICE E CAPA ------------------- */    

<INITIAL>^[ \t]*%index%          { addIndexLatex(); }                      /* Índice */
<INITIAL>^[ \t]*%cover%          { addCoverLatex(); }                      /* Capa */

    /* --------------------------- LISTAS -------------------------- */    

<INITIAL>%%ol%                   { BEGIN LIST;                             /* Começa o estado de lista */
                                   beginOrderListLatex();                  /* Início de lista ordenada */
                                 }                                        
<LIST>%ol%%                      { BEGIN INITIAL;                          /* Volta ao estado normal */
                                   endOrderListLatex();                    /* Fim de lista ordenada*/
                                 }                                         

<INITIAL>%%ul%                   { BEGIN LIST;                             /* Começa o estado de lista */
                                   beginUnorderListLatex();                /* Início de lista não ordenada */
                                 }
<LIST>%ul%%                      { BEGIN INITIAL;                          /* Volta ao estado normal */
                                   endUnorderListLatex();                  /* Fim de lista não ordenada */
                                 }

<INITIAL>%%dl%                   { BEGIN DICTIONARY;                       /* Começa o estado de dicionário*/
                                   beginDictionaryLatex();                 /* Início de entradas de um dicionário */
                                 }
<DICTIONARY>%dl%%                { BEGIN INITIAL;                          /* Volta ao estado normal */
                                   endDictionaryLatex();                   /* Fim de entradas de um dicionario */
                                 }

    /* --------------------------- ITEMS -------------------------- */    

<DICTIONARY>%it\{                { beginDictionaryEntryLatex(closeTags); } /* Início de um item de dicionário */  

<LIST>%it[ \t]                   { addItemLatex(); }                       /* Item de uma lista */ 

    /* --------------------------- IMAGENS -------------------------- */    

<INITIAL>%img\{                  { BEGIN IMG;                              /* Começa o estado de imagem */
                                   beginImageLatex(closeTags);             /* Tags de início de uma imagem */
                                 }

<IMG>,                           { endImagePathLatex();                    /* Fim do includegraphics */
                                   beginImageCaptionLatex();               /* Início da legenda */
                                   BEGIN INITIAL;                          /* Volta ao estado normal do Latex */
                                 }

    /* --------------------------- LINKS -------------------------- */    

<INITIAL>%link\{                 { BEGIN LINK;                             /* Começa o estado de link */
                                   beginLinkLatex(closeTags);              /* Tags de início de um link */
                                 }

<LINK>,                          { endURLLatex();                          /* Fim do url */
                                   beginLinkTextLatex();                   /* Início do texto do link */
                                   BEGIN INITIAL;                          /* Volta ao estado normal do Latex */
                                 }

    /* --------------------------- COMENTARIOS -------------------- */    

<INITIAL>%%%                     { addLineCommentLatex();                  /* Tag de comentário de linha */
                                   BEGIN COMMENT_LINE;                     /* Começa o estado de comentário */
                                 }
<COMMENT_LINE>\n                 { BEGIN INITIAL; }                        /* Volta ao estado normal*/

<INITIAL>%%comment%              { beginCommentLatex();                    /* Início de comentário */
                                   BEGIN COMMENT;                          /* Começa o estado de comentário */
                                 }
<COMMENT>%comment%%              { endCommentLatex();                      /* Fim de comentário */
                                   BEGIN INITIAL;                          /* Volta ao estado normal*/
                                 }

    /* --------------------------- VERBATIM ---------------------- */    

<INITIAL>%%verbatim%             { BEGIN VERB;                             /* Começa o estado verbatim */
                                   beginVerbatimLatex();                   /* Inicio de verbatim */
                                 }
<VERB>%verbatim%%                { BEGIN INITIAL;                          /* Volta ao estado normal do Latex */
                                   endVerbatimLatex();                     /* Fim de verbatim */
                                 }

<INITIAL>%%verb%                 { BEGIN VERB_LINE;                        /* Começa o estado verbatim de linha */
                                   beginVerbatimLineLatex();               /* Inicio de verbatim de linha */
                                 }
<VERB_LINE>%verb%%               { BEGIN INITIAL;                          /* Volta ao estado normal do Latex */
                                   endVerbatimLineLatex();                 /* Fim de verbatim de linha */
                                 }

    /* --------------------------- LATEX E HTML --------------------- */    

<INITIAL>%%latex%                { BEGIN LATEX; }                          /* Começa estado LATEX */
<LATEX>%latex%%                  { BEGIN INITIAL; }                        /* Volta ao estado normal */
                         
<INITIAL>%%html%(.|\n)*%html%%   { }                                       /* Ignora */


    /* --------------------------- TABELAS ------------------------- */    

<INITIAL>%%table%               { BEGIN TABLE; }                            /* Começa estado tabela */
<TABLE>%table%%                 { addTableLatex(table); }                   /* Cria tabela */
<TABLE>%head[ \t][^%\n]*        { newRowLatex(table,yytext+6,1); }          /* Nova linha header */
<TABLE>%row[ \t][^%\n]*         { newRowLatex(table,yytext+5,0); }          /* Nova linha */
<TABLE>%[ \t][^%\n]*            { newColumnLatex(table,yytext+1); }         /* Nova Coluna */

    /* --------------------------- GERAL --------------------------- */    

<INITIAL>\}                      { endLatexTag(closeTags); }               /* Fecha ultima tag aberta */
<*>.|\n                          { ECHO; }

%%

int main(){
    closeTags = createLinkedList(NULL,NULL);
    table = createLinkedList(NULL,NULL);
    yylex();
    return 0;
}

