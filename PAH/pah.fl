%option noyywrap
%{
    /* --------------------------- INCLUDES ----------------------- */    

    #include "functionsLatex.h"
    #include "utilities.h"
    #include "LinkedList/linkedlist.h"


    /* --------------------------- VAR GLOBAIS --------------------- */    

    LinkedList closeTags;   /* Lista ligada com as tags que estão por fechar */

%}
    /* --------------------------- VAR GLOBAIS --------------------- */  

%x LATEX         
%x HTML         
%x LATEX_VERB
%x LATEX_VERB_LINE
%x LATEX_COMMENT_LINE
%x LATEX_COMMENT
%x LATEX_IMG  
%x LATEX_LINK 
%x LATEX_ROW
%x LATEX_LIST


%%
    /* --------------------------- VARIAVEIS ----------------------- */    
%{   
    char delimiter;       /* Delimitador do verbatim de linha */    
%}
    /* --------------------------- TIPO DE DOC --------------------- */    
    
<INITIAL>^[ \t]*%html%             { BEGIN HTML; }   
<INITIAL>^[ \t]*%latex%            { BEGIN LATEX; } 

    /* --------------------------- LATEX --------------------------- */    

<LATEX>%b\{                    { beginBoldLatex(closeTags); }            /* Tag de início de negrito */
<LATEX>%i\{                    { beginItalicLatex(closeTags); }          /* Tag de início de italico */
<LATEX>%u\{                    { beginUnderlineLatex(closeTags); }       /* Tag de início de sublinhado */

<LATEX>%t[1-3]\{               { char* str = substring(yytext, 2, 1);    /* Tag de início de título */
                                 beginTitleLatex(closeTags,atoi(str));
                                 free(str) ;
                               }

<LATEX>^[ \t]*%index%          { addIndexLatex(); }                      /* Tag de índice */

<LATEX>%%ol%                   { beginOrderListLatex(); }                /* Tag de início de lista ordenada */
<LATEX>%ol%%                   { endOrderListLatex(); }                  /* Tag de fim de lista ordenada*/
<LATEX>%%ul%                   { beginUnorderListLatex(); }              /* Tag de início de lista não ordenada */
<LATEX>%ul%%                   { endUnorderListLatex(); }                /* Tag de fim de lista não ordenada */
<LATEX>%%dl%                   { beginDictionaryLatex(); }               /* Tag de início de entradas de um dicionário */
<LATEX>%dl%%                   { endDictionaryLatex(); }                 /* Tag de fim de entradas de um dicionario */

<LATEX>%it\{                   { beginDictionaryEntryLatex(closeTags); } /* Tag de início de um item de dicionário */  
<LATEX>%it[ \t]                { addItemLatex(); }                       /* Tag de item de uma lista */ 

<LATEX>%img\{                  { BEGIN LATEX_IMG;                        /* Começa o estado de imagem */
                                 beginImageLatex(closeTags);             /* Tags de início de uma imagem */
                               }

<LATEX_IMG>,                   { endImagePathLatex();                    /* Tag de fim do includegraphics */
                                 beginImageCaptionLatex();               /* Tag de início da legenda */
                                 BEGIN LATEX;                            /* Volta ao estado normal do Latex */
                               }

<LATEX>%link\{                 { BEGIN LATEX_LINK;                       /* Começa o estado de link */
                                 beginLinkLatex(closeTags);              /* Tags de início de um link */
                               }

<LATEX_LINK>,                  { endURLLatex();                          /* Tag de fim do url */
                                 beginLinkTextLatex();                   /* Tag de início do texto do link */
                                 BEGIN LATEX;                            /* Volta ao estado normal do Latex */
                               }

<LATEX>%%%                     { addLineCommentLatex();                  /* Tag de comentário de linha */
                                 BEGIN LATEX_COMMENT_LINE;               /* Começa o estado de comentário */
                               }
<LATEX>%%comment%              { beginCommentLatex();                    /* Tag de início de comentário */
                                 BEGIN LATEX_COMMENT;                    /* Começa o estado de comentário */
                               }
<LATEX_COMMENT_LINE>\n         { BEGIN LATEX; }                          /* Volta ao estado normal do Latex */
<LATEX_COMMENT>%comment%%      { endCommentLatex();                      /* Tag de fim de comentário */
                                 BEGIN LATEX;                            /* Volta ao estado normal do Latex */
                               }

<LATEX>%%verbatim%             { BEGIN LATEX_VERB;                       /* Começa o estado verbatim */
                                 beginVerbatimLatex();                   /* Tag de inicio de verbatim */
                               }
<LATEX_VERB>%verbatim%%        { BEGIN LATEX;                            /* Volta ao estado normal do Latex */
                                 endVerbatimLatex();                     /* Tag de fim de verbatim */
                               }

<LATEX>\}                      { endLatexTag(closeTags); }               /* Tag de fim geral */
    

    /* --------------------------- HTML ---------------------------- */    
    /* --------------------------- GERAL --------------------------- */    

<*>.|\n                        { printf("%s", yytext); }

%%

int main(){
    closeTags = createLinkedList(NULL,NULL);
    yylex();
    return 0;
}

